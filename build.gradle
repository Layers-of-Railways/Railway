plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1.+" apply false
    id "io.github.juuxel.loom-vineflower" version "1.+" apply false // Vineflower, a better decompiler
    id "me.shedaniel.unified-publishing" version "0.1.+" apply false // https://github.com/shedaniel/unified-publishing
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-vineflower"

    loom {
        silentMojangMappingsLicense()
        runs.configureEach {
            vmArg("-Dmixin.debug.export=true")
            vmArg("-Dmixin.env.remapRefMap=true")
            vmArg("-Dmixin.env.refMapRemappingFile=${projectDir}/build/createSrgToMcp/output.srg")
        }
    }

    repositories {
        mavenCentral()
        maven { url = "https://maven.shedaniel.me/" } // Cloth Config, REI
        maven { url = "https://maven.blamejared.com/" } // JEI, Hex Casting
        maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
        maven { url = "https://maven.quiltmc.org/repository/release" } // Quilt Mappings
        maven { url = "https://jm.gserv.me/repository/maven-public/" } // JourneyMap API
        maven { url = "https://api.modrinth.com/maven" } // LazyDFU, JourneyMap
        maven { // Flywheel
            url = "https://maven.tterrag.com/"
            content {
                // need to be specific here due to version overlaps
                includeGroup("com.jozufozu.flywheel")
            }
        }
        maven { // Extended Bogeys
            url = "https://maven.ithundxr.dev/private"
            content {
                includeGroup("com.rabbitminers")
            }
            authentication {
                basic(BasicAuthentication)
            }
            credentials(PasswordCredentials) {
                if (System.getenv("GITHUB_RUN_NUMBER")) {
                    username = "railways-github"
                    password = System.getenv("MAVEN_TOKEN")
                } else {
                    username = findProperty("IThundxrMavenUsername")
                    password = findProperty("IThundxrMavenPassword")
                }
            }
        }
        maven { url = 'https://maven.maxhenkel.de/repository/public' } // Simple Voice Chat
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        // layered mappings - Mojmap names, parchment and QM docs and parameters
        mappings(loom.layered {
            it.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${qm_version}:intermediary-v2")
            it.parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
            it.officialMojangMappings { nameSyntheticMembers = false }
        })

        // see each subproject for dependencies.
    }

    tasks.register("moveBuiltJars", Copy) {
            if (project.path != ":common") {
                def remapJar = project.tasks.named('remapJar')
                dependsOn remapJar
                from remapJar
            }

        into(rootProject.file("jars"))
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    group = rootProject.maven_group

    if (System.getenv("RELEASE_BUILD")) {
        version = "${mod_version}+${project.name}-mc${minecraft_version}"
    } else {
        // Formats the mod version to include the loader, Minecraft version, and build number (if present)
        // example: 1.0.0+fabric-1.18.2-100
        String buildNumber = System.getenv("GITHUB_RUN_NUMBER")
        version = "${mod_version}+${project.name}-mc${minecraft_version}" + (buildNumber != null ? "-build.${buildNumber}" : "")
    }


    repositories {
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency {
            if (requested.module.name == "fabric-loader") {
                useVersion(fabric_loader_version)
            }
        }
    }
}

// 'borrowed' from Create
String getChangelogText(String loader, boolean recent) {
    def changelogFile = file(recent ? 'recent_changelog.txt' : 'changelog.txt')
    String str = ''
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (it.size() > 1) {
            def temp = it
            if (lineCount == 0) {
                temp = "Steam 'n' Rails ${version}"
                temp = "<span style=\"font-size: 18px; color: #333399;\">Steam 'n' Rails v${mod_version}</span>&nbsp;&nbsp;<em>for Minecraft $loader ${minecraft_version}</em><br/>"
            } else if (it.startsWith('-')) {
                temp = "&nbsp;&nbsp;&nbsp;$temp<br/>"
                temp = temp.replaceAll("(\\S+\\/\\S+)#([0-9]+)\\b", "<a href=\"https://github.com/\$1/issues/\$2\">\$0</a>");
                temp = temp.replaceAll("#([0-9]+)\\b(?!</a>)", "<a href=\"https://github.com/Layers-of-Railways/Railway/issues/\$1\">\$0</a>");
            } else {
                temp = "<h4>$temp</h4>"
            }
            str += temp
            lineCount++
        } else {
            str += "<p>Please submit any Issues you come across on the&nbsp;<a href=\"https://github.com/Layers-of-Railways/Railway/issues\" rel=\"nofollow\">Issue Tracker</a>.</p>"
            done = true
        }
    }
    return str
}
new File("changelog.html").write getChangelogText("Fabric", false)
new File("recent_changelog.html").write getChangelogText("Fabric", true)
