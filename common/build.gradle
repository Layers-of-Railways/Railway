import groovy.json.JsonOutput
import groovy.json.JsonSlurper

loom {
    accessWidenerPath = file("src/main/resources/railways.accesswidener")
}

repositories {
    // mavens for Create Fabric and dependencies
    maven { url = "https://api.modrinth.com/maven" } // LazyDFU, Journyemap
    maven { url = "https://maven.terraformersmc.com/releases/" } // Mod Menu
    maven { url = "https://mvn.devos.one/snapshots/" } // Create Fabric, Porting Lib, Forge Tags, Milk Lib, Registrate Fabric
    maven { url = "https://cursemaven.com" } // Forge Config API Port
    maven { url = "https://maven.cafeteria.dev/releases" } // Fake Player API
    maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
    maven { url = "https://jitpack.io/" } // Mixin Extras, Fabric ASM
    maven { url = "https://maven.blamejared.com/" } // JEI, Hex Casting
}

dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation("net.fabricmc:fabric-loader:${fabric_loader_version}")
    // Compile against Create Fabric in common
    // beware of differences across platforms!
    // dependencies must also be pulled in to minimize problems, from remapping issues to compile errors.
    // All dependencies except Flywheel and Registrate are NOT safe to use!
    // Flywheel and Registrate must also be used carefully due to differences.
    modCompileOnly("com.simibubi.create:create-fabric-${minecraft_version}:${create_fabric_version}")

    // required for proper remapping and compiling
    modCompileOnly("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")

    // JourneyMap compat
    modCompileOnly("info.journeymap:journeymap-api:${journeymap_api_version}-fabric-SNAPSHOT")

    modCompileOnly "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"
    modCompileOnly "maven.modrinth:simple-voice-chat:fabric-${voicechat_version}"

    // mod compat for tracks

    // Hex Casting
/*    modCompileOnly("at.petra-k.paucal:paucal-common-${minecraft_version}:${paucal_version}")
    modCompileOnly("at.petra-k.hexcasting:hexcasting-common-${minecraft_version}:${hexcasting_version}")
    modCompileOnly("vazkii.patchouli:Patchouli-xplat:${minecraft_version}-${patchouli_version}")*/

    if (enable_eb.toBoolean())
        modImplementation("com.rabbitminers:extendedbogeys-common:${EB_verison}+common-patch-")

    implementation("com.github.llamalad7.mixinextras:mixinextras-common:${mixin_extras_version}")
    annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-common:${mixin_extras_version}")
}

architectury {
    common(rootProject.enabled_platforms.split(","))
}

processResources {
    exclude("resourcepacks/", "**/*.bbmodel", "**/*.lnk", "**/*.txt") // must be part of primary mod to be findable, dont add development or to-do files into built jar

    // Minify all .json files in built jars
    doLast {
        fileTree(dir: processResources.outputs.files.asPath, include: '**/*.json').each {
            it.text = JsonOutput.toJson(new JsonSlurper().parse(it))
        }
    }
}

sourceSets.main {
    resources { // include generated resources in resources
        srcDir("src/generated/resources")
        exclude(".cache/**")
        exclude("assets/create/**")
    }
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            artifactId = "${archives_base_name}-${project.name}-${minecraft_version}"
            from components.java
        }
    }

    repositories {
        if (System.getenv("MAVEN_TOKEN")) {
            maven {
                url "https://maven.ithundxr.dev/releases"
                credentials {
                    username = "railways-github"
                    password = System.getenv("MAVEN_TOKEN")
                }
            }
        }
    }
}